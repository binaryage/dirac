domain symbol_server
  # Error details
  type Error extends object
    properties
      # An error code
      enum code
        internal_error
        not_found
      # The error message
      string message


  type RawModule extends object
    properties
      # The origin url for the raw module, if it exists
      optional string url
      # The source or bytecode defining the JS script or wasm module
      optional binary code

  # Notify the plugin about a new script
  command addRawModule
    parameters
      # A raw module identifier
      string rawModuleId
      # A URL to file containing symbols in a plugin-specific format.
      optional string symbols
      # The new raw module
      RawModule rawModule
    returns
      # The original source files the raw module was compiled from.
      # Filenames are in URL format
      array of string sources
      # Error details if the raw module couldn't be handled
      optional Error error

  # Offsets in raw modules
  type RawLocation extends object
    properties
      # Module identifier
      string rawModuleId
      # Offset of the location in the raw module
      integer codeOffset

  # Locations in source files
  type SourceLocation extends object
    properties
      # Module identifier
      string rawModuleId
      # Url of the source file
      string sourceFile
      # Line number of the location in the source file
      integer lineNumber
      # Column number of the location in the source file
      integer columnNumber

  # Find locations in raw modules from a location in a source file
  command sourceLocationToRawLocation
    parameters
      # Module identifier
      string rawModuleId
      # Url of the source file
      string sourceFile
      # Line number of the location in the source file
      integer lineNumber
      # Column number of the location in the source file
      integer columnNumber
    returns
      # The raw locations matching the source locations
      array of RawLocation rawLocation
      # Error details if the command failed
      optional Error error

  # Find locations in source files from a location in a raw module
  command rawLocationToSourceLocation
    parameters
      # Module identifier
      string rawModuleId
      # Offset of the location in the raw module
      integer codeOffset
    returns
      # The source locations matching the raw locations
      array of SourceLocation sourceLocation
      # Error details if the command failed
      optional Error error

  # A source language variable
  type Variable extends object
    properties
      # Scope of the variable
      enum scope
        local
        parameter
        global
      # Name of the variable
      string name
      # Type of the variable
      string type

  # List all variables in lexical scope at a location in a raw module
  command listVariablesInScope
    parameters
      # Module identifier
      string rawModuleId
      # Offset of the location in the raw module
      integer codeOffset
    returns
      # The variables present in the scope
      array of Variable variable
      # Error details if the command failed
      optional Error error

  # Evaluate the content of a variable in a given lexical scope
  command evaluateVariable
    parameters
      # Name of the variable to look up
      string name
      # The lexical scope to evaluate the variable
      RawLocation location
    returns
      # A raw module containing wasm bytecode that, when called, will
      # render the variable as a json with keys `name`, `type`, `value`
      RawModule value
      # error details if the command failed
      optional Error error
