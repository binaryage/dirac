# Copyright 2020 The Chromium Authors.  All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../../../third_party/typescript/typescript.gni")
import("./copy.gni")
import("./vars.gni")

template("devtools_module") {
  _typescript_target_name = target_name + "-typescript"
  _typescript_target_dep = ":$_typescript_target_name"

  ts_library(_typescript_target_name) {
    sources = invoker.sources
    deps = invoker.deps

    # To make sure that this target can be depended upon by other TypeScript targets,
    # we have to name it the `target_name`. However, we can't name this `ts_library` the
    # `target_name`, as the buildgraph requires the `copy_gen__to_resources` to happen *after*
    # `ts_library` is finished.
    typescript_config_name = invoker.target_name

    is_web_worker = defined(invoker.is_web_worker) && invoker.is_web_worker
  }

  if (is_debug) {
    copy_gen_to_resources(target_name) {
      sources =
          filter_include(get_target_outputs(_typescript_target_dep), [ "*.js" ])

      public_deps = [ _typescript_target_dep ]

      visibility = invoker.visibility + [ ":*" ]
    }
  } else {
    group(target_name) {
      public_deps = [ _typescript_target_dep ]

      visibility = invoker.visibility + [ ":*" ]
    }
  }
}

set_defaults("devtools_module") {
  visibility = []
}
