# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../third_party/typescript/typescript.gni")

resources_out_dir = "$root_out_dir/resources/inspector_overlay"

action("build_inspector_overlay") {
  script = "../scripts/build/build_inspector_overlay.py"

  inputs = [
    "$target_gen_dir/common.css",
    "$target_gen_dir/common.js",
    "$target_gen_dir/css_grid_label_helpers.js",
    "$target_gen_dir/highlight_common.js",
    "$target_gen_dir/highlight_grid_common.js",
    "$target_gen_dir/tool_distances_impl.js",
    "$target_gen_dir/tool_distances.js",
    "$target_gen_dir/tool_highlight_grid_impl.js",
    "$target_gen_dir/tool_highlight_grid.css",
    "$target_gen_dir/tool_highlight_grid.js",
    "$target_gen_dir/tool_highlight_impl.js",
    "$target_gen_dir/tool_highlight.css",
    "$target_gen_dir/tool_highlight.js",
    "$target_gen_dir/tool_paused_impl.js",
    "$target_gen_dir/tool_paused.css",
    "$target_gen_dir/tool_paused.js",
    "$target_gen_dir/tool_screenshot_impl.js",
    "$target_gen_dir/tool_screenshot.css",
    "$target_gen_dir/tool_screenshot.js",
    "$target_gen_dir/tool_source_order_impl.js",
    "$target_gen_dir/tool_source_order.css",
    "$target_gen_dir/tool_source_order.js",
    "$target_gen_dir/tool_viewport_size_impl.js",
    "$target_gen_dir/tool_viewport_size.js",
  ]

  outputs = [
    "$resources_out_dir/common.css",
    "$resources_out_dir/tool_distances.js",
    "$resources_out_dir/tool_highlight_grid.js",
    "$resources_out_dir/tool_highlight.js",
    "$resources_out_dir/tool_paused.js",
    "$resources_out_dir/tool_screenshot.js",
    "$resources_out_dir/tool_source_order.js",
    "$resources_out_dir/tool_viewport_size.js",
  ]

  args = [
    "common.css",
    "1200",  # max_size in bytes
    "tool_highlight.js",
    "90100",  # max_size in bytes
    "tool_highlight_grid.js",
    "70000",  # max_size in bytes
    "tool_distances.js",
    "5000",  # max_size in bytes
    "tool_paused.js",
    "10000",  # max_size in bytes
    "tool_screenshot.js",
    "5000",  # max_size in bytes
    "tool_viewport_size.js",
    "5000",  # max_size in bytes
    "tool_source_order.js",
    "11000",  # max_size in bytes
    "--input_path",
    rebase_path(target_gen_dir, root_build_dir),
    "--output_path",
    rebase_path(resources_out_dir, root_build_dir),
    "--rollup_plugin",
    rebase_path("loadCSS.rollup.js", root_build_dir),
  ]

  public_deps = [
    ":copy_css_to_gen",
    ":copy_resources",
    ":inspector_overlay",
  ]
}

copy("copy_resources") {
  sources = [ "resources.grd" ]
  outputs = [ "$resources_out_dir/inspector_overlay_resources.grd" ]
}

copy("copy_css_to_gen") {
  sources = [
    "common.css",
    "tool_highlight.css",
    "tool_highlight_grid.css",
    "tool_paused.css",
    "tool_screenshot.css",
    "tool_source_order.css",
  ]
  outputs = [ "$target_gen_dir/{{source_file_part}}" ]
}

ts_library("inspector_overlay") {
  sources = [
    "common.ts",
    "css_grid_label_helpers.ts",
    "highlight_common.ts",
    "highlight_grid_common.ts",
    "tool_distances.ts",
    "tool_distances_impl.ts",
    "tool_highlight.ts",
    "tool_highlight_grid.ts",
    "tool_highlight_grid_impl.ts",
    "tool_highlight_impl.ts",
    "tool_paused.ts",
    "tool_paused_impl.ts",
    "tool_screenshot.ts",
    "tool_screenshot_impl.ts",
    "tool_source_order.ts",
    "tool_source_order_impl.ts",
    "tool_viewport_size.ts",
    "tool_viewport_size_impl.ts",
  ]

  deps = [ "../front_end/common:bundle" ]
}
