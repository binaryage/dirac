# Copyright 2020 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("../../third_party/typescript/typescript.gni")

resources_out_dir = "$root_out_dir/resources/inspector_overlay"

action("build_inspector_overlay") {
  script = "../../scripts/build/build_inspector_overlay.py"

  inputs = [
    "./common.css",
    "$target_gen_dir/common.js",
    "$target_gen_dir/css_grid_label_helpers.js",
    "$target_gen_dir/highlight_common.js",
    "$target_gen_dir/highlight_grid_common.js",
    "$target_gen_dir/tool_highlight_impl.js",
    "$target_gen_dir/tool_highlight.js",
    "$target_gen_dir/tool_highlight_grid_impl.js",
    "$target_gen_dir/tool_highlight_grid.js",
    "$target_gen_dir/tool_paused_impl.js",
    "$target_gen_dir/tool_paused.js",
    "$target_gen_dir/tool_screenshot_impl.js",
    "$target_gen_dir/tool_screenshot.js",
    "$target_gen_dir/tool_viewport_size_impl.js",
    "$target_gen_dir/tool_viewport_size.js",
    "$target_gen_dir/tool_distances_impl.js",
    "$target_gen_dir/tool_distances.js",
  ]

  outputs = [
    "$resources_out_dir/common.css",
    "$resources_out_dir/tool_highlight.js",
    "$resources_out_dir/tool_highlight_grid.js",
    "$resources_out_dir/tool_distances.js",
    "$resources_out_dir/tool_paused.js",
    "$resources_out_dir/tool_screenshot.js",
    "$resources_out_dir/tool_viewport_size.js",
  ]

  args = [
    "common.css",
    "1200",  # max_size in bytes
    "tool_highlight.js",
    "50000",  # max_size in bytes
    "tool_highlight_grid.js",
    "25000",  # max_size in bytes
    "tool_distances.js",
    "5000",  # max_size in bytes
    "tool_paused.js",
    "10000",  # max_size in bytes
    "tool_screenshot.js",
    "5000",  # max_size in bytes
    "tool_viewport_size.js",
    "5000",  # max_size in bytes
    "--input_path",
    rebase_path(".", root_build_dir),
    "--output_path",
    rebase_path(resources_out_dir, root_build_dir),
  ]

  public_deps = [
    ":copy_resources",
    ":inspector_overlay",
  ]
}

copy("copy_resources") {
  sources = [ "resources.grd" ]
  outputs = [ "$resources_out_dir/inspector_overlay_resources.grd" ]
}

ts_library("inspector_overlay") {
  sources = [
    "common.js",
    "css_grid_label_helpers.js",
    "highlight_common.js",
    "highlight_grid_common.js",
    "tool_distances.js",
    "tool_distances_impl.js",
    "tool_highlight.js",
    "tool_highlight_grid.js",
    "tool_highlight_grid_impl.js",
    "tool_highlight_impl.js",
    "tool_paused.js",
    "tool_paused_impl.js",
    "tool_screenshot.js",
    "tool_screenshot_impl.js",
    "tool_viewport_size.js",
    "tool_viewport_size_impl.js",
  ]
  deps = [ "../common:bundle" ]
}
